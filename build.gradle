buildscript {
    repositories {
      jcenter()
    }

    dependencies {
		    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        jcenter()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.21'

        testCompile 'junit:junit:4.12'
    }

    ext {
        deploy = false
        travisEnabled = System.getenv('TRAVIS') ? true : false
        travisTag = System.getenv('TRAVIS_TAG')
        travisBuildNumber = System.getenv('TRAVIS_BUILD_NUMBER')

        bintrayUser = System.getenv('BINTRAY_USER')
        bintrayApiKey = System.getenv('BINTRAY_API_KEY')
    }

    group 'com.inkycode.nexus'
    version 'development'
    if (project.travisEnabled) {
        version = project.travisTag ? project.travisTag : "development-${project.travisBuildNumber}"
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            mavenJava (MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = project.bintrayUser
        key = project.bintrayApiKey

        publications = ['mavenJava']
        //configurations = ['archives']

        //dryRun = project.travisTag ? false : true
        publish = project.travisTag ? true : false

        pkg {
            repo = 'maven'
            name = project.group
            licenses = ['MIT']
            websiteUrl = 'https://github.com/inkycode/nexus'
            issueTrackerUrl = 'https://github.com/inkycode/nexus/issues'
            vcsUrl = 'https://github.com/inkycode/nexus.git'
            version {
                name = project.version
                desc = "Nexus ${project.archivesBaseName}-${project.version}"
                released = new Date()
            }
        }
    }
}
